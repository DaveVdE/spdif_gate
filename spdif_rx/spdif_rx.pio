; Copyright (c) 2023, Elehobica
; Released under the BSD-2-Clause
; refer to https://opensource.org/licenses/BSD-2-Clause

; === SPDIF Specification ===
; [Bit] (1 bit = 2 symbols)
; BMC (Bitphase Mark Code)
;          prev   bit0   bit1
; symbol   X 0    1 1    1 0
; symbol   X 1    0 0    0 1
; 
; [Sync] not encoded in BMC (Sync Code = 8 symbols)
; Sync Code B: symbol 1 1 1 0 1 0 0 0 (0xE8), 0 0 0 1 0 1 1 1 (0x17)
; Sync Code M: symbol 1 1 1 0 0 0 1 0 (0xE2), 0 0 0 1 1 1 0 1 (0x1D)
; Sync Code W: symbol 1 1 1 0 0 1 0 0 (0xE4), 0 0 0 1 1 0 1 1 (0x1B)
;
; [SPDIF Sub Frame Format] (64 symbols)
; 16bit Audio Format
; Sync (8) + AUX (8) + '0' (8) + 16bit Audio (32) + VUCP (8)
; 24bit Audio Format
; Sync (8) + 24bit Audio (48) + VUCP (8)
;
; [SPDIF Frame Format] (2 Sub Frames)
; B(L) + W(R) : head block
; M(L) + W(R) : succeeding blocks

; [SPDIF Block Format] (192 Frames = 384 Sub Frames)
; B(L0) + W(R0) + M(L1) + W(R1) + M(L2) + W(R2) + .... + M(L191) + W(R191)

; === spdif_rx PIO program Specification ===
; pio frequency: 96 MHz (sys_clk)
; supported input sampling frequency: 44.1 MHz
; -> symbol clock: 5.6448 MHz (128fs) (17 cycles of sys_clk)
; pio output bit rate: 5.6448 Mbps (No bit loss regardless of the diff between input 44.1KHz and rp2040's 44.1KHz)

; pio outputs (bit order is opposite to SPDIF format)
;
; 16bit Audio Format
; PCUV (4) + 16bit Audio (16) + '0' (4) + AUX (4) + Sync (4)
; 24bit Audio Format
; PCUV (4) + 24bit Audio (24) + Sync (4)
;
;  0b0  : bit0 (symbol_00 or symbol_11)
;  0b1  : bit1 (symbol_10 or symbol_01)
;  0b11 : syncE (symbol_1110) or sync8 (symbol_1000)  (can't distinguish)
;
;   thus Sync Code will consist of as follows
;   0b1111 : Sync Code B (sync E8) (syncE -> sync8)
;   0b1011 : Sync Code M (sync E2) (syncE -> bit0 -> bit1)
;   0b0111 : Sync Code W (sync E4) (syncE -> bit1 -> bit0)

; ==============================================================================================
.program spdif_rx
; it's desirable symbol cycle should meet with sampling frequency of input source
;  at least between 2+1 cycles, then it should keep good latching position of the symbol.
;  (because 'wait' intruction can cancel the accumulated timing error)
.define cy 21     ; symbol cycle for both 44.1 KHz and 48 KHz(for reference, cy = 22.14 for 44.1 KHz, cy = 20.34 for 48 KHz)
.define lp cy/2   ; symbol latch point (0 ~ cy-1)

; assuming y, osr registers are configured as below
;   y  : 0x00000000
;   osr: 0x00000003

public entry_point:
    wait 0 pin 0

;                          ; comment                       :  start sym pos   end sym pos
;                                                             (*) symbol latch with delay
.wrap_target
wait1:
    wait 1 pin 0 [cy-1+lp] ; wait for 0 -> 1               :  0                1 + lp/cy
symbol_1x:
    jmp pin symbol_11x     ; If symbol 11 -> go symbol_11x :  1 + lp/cy (*)    1 + (lp+1)/cy
    in osr, 1              ; else emit 1 (symbol 10)       :  1 + (lp+1)/cy    1 + (lp+2)/cy
    jmp wait1              ; go wait1                      :  1 + (lp+2)/cy    1 + (lp+3)/cy
symbol_11x:
    nop [cy-2]             ;                               :  1 + (lp+1)/cy    2 + lp/cy
    jmp pin sync1110       ; If symbol 111 -> go sync1110  :  2 + lp/cy (*)    2 + (lp+1)/cy (maximum delayed latch point)
    in y, 1                ; else emit 0 (symbol 110)      :  2 + (lp+1)/cy    2 + (lp+2)/cy
    jmp symbol_0x [cy-3]   ; go symbol_0x                  :  2 + (lp+2)/cy    3 + lp/cy (= 1 + lp/cy)

wait0pre:
    wait 1 pin 0

wait0:
    wait 0 pin 0 [cy-1+lp] ; wait for 1 -> 0               :  0                1 + lp/cy
symbol_0x:
    jmp pin symbol_01      ; If symbol 01 -> go symbol_01  :  1 + lp/cy (*)    1 + (lp+1)/cy
    in y, 1                ; else emit 0 (symbol 00)       :  1 + (lp+1)/cy    1 + (lp+2)/cy
.wrap
symbol_01:
    in osr, 1              ; emit 1                        :  1 + (lp+1)/cy    1 + (lp+2)/cy
    jmp wait0              ; go wait0                      :  1 + (lp+2)/cy    1 + (lp+3)/cy

sync1110:
    push block             ; this is 32bit start
    in osr, 2              ; emit sync (sync 1110)         :  2 + (lp+1)/cy    2 + (lp+2)/cy
    wait 0 pin 0 [cy-1+lp] ; wait for 1 -> 0               :  3                4 + lp/cy
    jmp pin sync1xxx [cy-2]; If sync 1xxx -> go sync1xxx   :  4 + lp/cy (*)    5 + (lp-1)/cy
    jmp symbol_0x          ; else go symbol_0x (sync 0xxx) :  5 + (lp-1)/cy    5 + lp/cy (= 1 + lp/cy)

sync1xxx:                  ; sync1xxx must be sync1000
    in osr, 2              ; emit sync (sync 1000)         :  2 + (lp-1)/cy    2 + lp/cy
    jmp entry_point        ;                               :  2 + lp/cy        2 + (lp+1)/cy

; ==============================================================================================
.program spdif_rx_inv
; it's desirable symbol cycle should meet with sampling frequency of input source
;  at least between 2+1 cycles, then it should keep good latching position of the symbol.
;  (because 'wait' intruction can cancel the accumulated timing error)
.define cy 21     ; symbol cycle for both 44.1 KHz and 48 KHz(for reference, cy = 22.14 for 44.1 KHz, cy = 20.34 for 48 KHz)
.define lp cy/2   ; symbol latch point (0 ~ cy-1)

; assuming y, osr registers are configured as below
;   y  : 0x00000000
;   osr: 0x00000003

;                          ; comment                       :  start sym pos   end sym pos
;                                                             (*) symbol latch with delay
wait1:
    wait 1 pin 0 [cy-1+lp] ; wait for 0 -> 1               :  0                1 + lp/cy
.wrap_target
symbol_1x:
    jmp pin symbol_11      ; If symbol 11 -> go symbol_11  :  1 + lp/cy (*)    1 + (lp+1)/cy
    in osr, 1              ; else emit 1 (symbol 10)       :  1 + (lp+1)/cy    1 + (lp+2)/cy
    jmp wait1              ; go wait1                      :  1 + (lp+2)/cy    1 + (lp+3)/cy
symbol_11 :
    in y, 1                ; emit 0 (symbol 11)            :  1 + (lp+1)/cy    1 + (lp+2)/cy
    jmp wait0              ; go wait0                      ;  1 + (lp+2)/cy    1 + (lp+3)/cy

public entry_point:
wait0pre:
    wait 1 pin 0

wait0:
    wait 0 pin 0 [cy-1+lp] ; wait for 1 -> 0               :  0                1 + lp/cy
symbol_0x:
    jmp pin symbol_01      ; If symbol 01 -> go symbol_01  :  1 + lp/cy (*)    1 + (lp+1)/cy
symbol_00x:
    nop [cy-2]             ;                               :  1 + (lp+1)/cy    2 + lp/cy
    jmp pin symbol_00      ; If symbol 001 -> go symbol_00 :  2 + lp/cy (*)    2 + (lp+1)/cy
sync0001:
    push block             ; this is 32bit start
    in osr, 2              ; emit sync (sync 0001)         :  2 + (lp+1)/cy    2 + (lp+2)/cy
    wait 1 pin 0 [cy-1+lp] ; wait for 0 -> 1               :  3                4 + lp/cy
    jmp pin symbol_1x [cy-1]; If sync 1xxx -> go symbol_1x :  4 + lp/cy (*)    5 + lp/cy (= 1 + lp/cy)

sync0xxx:                  ; sync0xxx must be sync0111 (no check)
    in osr, 2              ; emit sync (sync 0111)         :  5 + lp/cy        5 + (lp+1)/cy
    jmp wait0pre           ;                               :  5 + (lp+1)/cy    5 + (lp+2)/cy

symbol_00:
    in y, 1 [cy-2]         ; emit 0 (symbol 001)           :  2 + (lp+1)/cy    3 + lp/cy (= 1 + lp/cy)
.wrap

symbol_01:
    in osr, 1              ; emit 1                        :  1 + (lp+1)/cy    1 + (lp+2)/cy
    jmp wait0              ; go wait0                      :  1 + (lp+2)/cy    1 + (lp+3)/cy

; ==============================================================================================
% c-sdk {

static inline void spdif_rx_program_init(PIO pio, uint sm, uint offset, uint entry_point, pio_sm_config (*get_default_config)(uint), uint pin) {
    pio_sm_set_consecutive_pindirs(pio, sm, pin, 1, false);
    pio_gpio_init(pio, pin);
    gpio_pull_down(pin);

    pio_sm_config sm_config = get_default_config(offset);

    sm_config_set_jmp_pin(&sm_config, pin);
    sm_config_set_in_pins(&sm_config, pin); // PINCTRL_IN_BASE for wait
    sm_config_set_in_shift(&sm_config, true, false, 32); // shift_right, no autopush, 32bit

    pio_sm_init(pio, sm, offset, &sm_config);
    pio_sm_set_pins(pio, sm, 0); // clear pins

    // set y, OSR (use as config value)
    pio_sm_set_enabled(pio, sm, false);
    pio_sm_put_blocking(pio, sm, 0x0); // y = 0x0
    pio_sm_exec(pio, sm, pio_encode_pull(false, false));
    pio_sm_exec(pio, sm, pio_encode_out(pio_y, 32));
    pio_sm_put_blocking(pio, sm, 0x3); // osr = 0x3
    pio_sm_exec(pio, sm, pio_encode_pull(false, false)); // only pull to store to osr
    pio_sm_set_enabled(pio, sm, true);

    // fifo join needs to be done after pull/out
    sm_config_set_fifo_join(&sm_config, PIO_FIFO_JOIN_RX);

    pio_sm_exec(pio, sm, pio_encode_jmp(offset + entry_point));
}

static inline uint32_t spdif_rx_program_get32(PIO pio, uint sm) {
    // 32-bit read from the FIFO
    while (pio_sm_is_rx_fifo_empty(pio, sm)) {
        tight_loop_contents();
    }
    return (uint32_t) pio->rxf[sm];
}

%}